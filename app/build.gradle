apply plugin: 'com.android.application'

def gitTag() {
    def cmd = "git describe --abbrev=0 --tags"
    def proc = cmd.execute()
    return proc.text.trim()
}

def gitCommitNr() {
    def cmd = "git rev-list HEAD --count"
    def proc = cmd.execute()
    return proc.text.trim().toInteger()
}

def verNameFull() {
    return (gitTag() ? gitTag() + '.' + gitCommitNr().toString() : "NO_VERSION").toString()
}

android {
    compileSdkVersion 25
    buildToolsVersion '23.0.3'

    defaultConfig {
        applicationId "hr.duby.rcmower"
        minSdkVersion 15
        targetSdkVersion 24
        versionCode gitCommitNr()
        versionName verNameFull()
    }
    buildTypes {
        buildTypes.each {
            it.buildConfigField 'String', 'app_version', "\"" + verNameFull() + "\""
            it.resValue 'string', 'app_version', "\"" + verNameFull() + "\""
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    useLibrary 'org.apache.http.legacy'

}


dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.support:appcompat-v7:24.2.1'
    compile 'com.android.support.constraint:constraint-layout:1.0.0-alpha4'
    testCompile 'junit:junit:4.12'
    compile 'org.java-websocket:Java-WebSocket:1.3.0'
}
